/*
 *  Build file for gradle
 */


plugins {
    id 'com.palantir.git-version' version '3.0.0'
//    id 'maven-publish'
//    id 'signing'
}


apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'


java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(8))
  }
}

repositories {
	maven {		// sonatype repository
		url "https://repo1.maven.org/maven2/"
	}
    mavenCentral()
}

println "Using java version " + JavaVersion.current()

sourceSets {
    main {    	
        java {
            srcDirs 'src'            
        }
        
        resources {
			srcDirs= ['src']
			exclude '**/*.java'
		}
		
		output.resourcesDir = "$buildDir/classes/java/$name"
    }

    test {
        java {
            srcDirs 'test'
        }
                
        resources {
			srcDirs= ['test']
			exclude '**/*.java'
		}
		
		output.resourcesDir = "$buildDir/classes/java/$name"
    }               
}

configurations {
	catImplementation {
		extendsFrom implementation
		description = 'Main implementation'
		canBeResolved = true
	}
}

dependencies {
    implementation 'org.jfree:jcommon:1.0.23'
    implementation 'org.jfree:jfreechart:1.0.17'
    implementation 'jfree:jfreechart-swt:1.0.17'
    implementation 'net.sf.py4j:py4j:0.10.9.2' 
    implementation 'com.cedarsoftware:json-io:4.13.0'
       
   	implementation group: 'io.github.cwfc-ccfb', name: 'repicea', version: '[1.9.8,)'    
    implementation group: 'io.github.cwfc-ccfb', name: 'repicea-mathstats', version: '[1.2.2,)'    
    implementation group: 'io.github.cwfc-ccfb', name: 'repicea-simulation', version: '[1.0.6,)'    
    implementation group: 'io.github.cwfc-ccfb', name: 'lerfobforesttools', version: '[1.4.4,)'    
    
    implementation group: 'org.apache.xmlgraphics', name: 'batik-svggen', version: '1.14'
	implementation group: 'org.apache.xmlgraphics', name: 'batik-dom', version: '1.14'
	implementation group: 'org.apache.xmlgraphics', name: 'batik-codec', version: '1.14'
    
    testImplementation "junit:junit:4.12"    
}

compileJava {
	project.version= gitVersion() 
	println "Building " + rootProject.name + " " + project.version
	println "Installation directory " + "../externallibraries/" + rootProject.name
}

test {
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true

    // set heap size for the test JVM(s)
    maxHeapSize = "2048m"
    
    exclude '**/ProductionEnvironmentTest.class'
}

jar {
	manifest {
        attributes("Built-By": "Mathieu Fortin",
        		   "Specification-Title": "Carbon Accounting Tool",
        		   "Specification-Version": archiveVersion,                   
                   "Specification-Vendor": "LERFOB and Canadian Wood Fibre Centre",
                   "Implementation-Title": "cat",
                   "Implementation-Vendor": "LERFOB and Canadian Wood Fibre Centre",
                   )
    }
    
	into ('/') {
		from files(['./README.md', './LICENSE.md'])			
	}
	
	// include all java files from main sourceset into the jar file (resources are already included)
	from sourceSets.main.allJava		
}

task fatJar(type: Jar) {
	group = 'build'
	description = 'Creates a fat jar for distribution'
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE		// to avoid duplicate files
    manifest {
         attributes("Built-By": "Mathieu Fortin",
        		   "Specification-Title": "Carbon Accounting Tool",
        		   "Specification-Version": archiveVersion,                   
                   "Specification-Vendor": "LERFOB and Canadian Wood Fibre Centre",
                   "Implementation-Title": "cat",
                   "Implementation-Vendor": "LERFOB and Canadian Wood Fibre Centre",
                   "Main-Class": "lerfob.carbonbalancetool.Starter")
    }
    archiveBaseName = 'CAT'
	destinationDirectory =  file("../externallibraries/" + rootProject.name)  
    from { sourceSets.main.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }		
}



task integrationTest(type: Test) {
	description = 'Runs the integration tests.'
    group = 'verification'
	dependsOn jar
	
	include '**/*.class'
	
	testLogging.showStandardStreams = true
    testClassesDirs = sourceSets.test.output.classesDirs
        
    // set classpath to test classes + test dependencies + JAR
    classpath = project.sourceSets.test.output + configurations.testRuntimeClasspath + files(jar.archiveFile) 
    outputs.upToDateWhen { false }
    mustRunAfter test
    maxHeapSize = "2048m"
}

check.dependsOn integrationTest

task deleteInstallFolder(type: Delete) {
  delete "../externallibraries/" + rootProject.name
}

task install(type: Copy) {
	description = 'Copies the output jar to installation folder.'
	group = 'build'
	dependsOn deleteInstallFolder,integrationTest
	from configurations.catImplementation
	from jar
	into "../externallibraries/" + rootProject.name
}